buildscript {
  ext.versions = [
    'kotlin': '1.4.10',
    'serialization': '1.0.0',
    'retrofit': '2.6.4',
    'okhttp': '3.14.7',
    'junit': '4.13',
    'truth': '1.0.1',
  ]
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
    classpath 'com.diffplug.spotless:spotless-plugin-gradle:5.6.1'
  }
}

apply plugin: 'java-library'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply from: file('gradle/gradle-mvn-push.gradle')

targetCompatibility = JavaVersion.VERSION_1_6
sourceCompatibility = JavaVersion.VERSION_1_6

repositories {
  mavenCentral()
}

dependencies {
  api "com.squareup.retrofit2:retrofit:${versions.retrofit}"
  api "org.jetbrains.kotlinx:kotlinx-serialization-core:${versions.serialization}"

  testImplementation "junit:junit:${versions.junit}"
  testImplementation "com.squareup.okhttp3:mockwebserver:${versions.okhttp}"
  testImplementation "com.google.truth:truth:${versions.truth}"
  testImplementation "org.jetbrains.kotlinx:kotlinx-serialization-protobuf:${versions.serialization}"
  testImplementation "org.jetbrains.kotlinx:kotlinx-serialization-json:${versions.serialization}"
}

tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
  task.kotlinOptions {
    freeCompilerArgs += [
        '-Xopt-in=kotlin.RequiresOptIn',
        '-Xjsr305=strict',
    ]
  }
}

compileTestKotlin {
  kotlinOptions {
    freeCompilerArgs += [
        '-Xopt-in=kotlinx.serialization.ExperimentalSerializationApi',
    ]
  }
}

apply plugin: 'com.diffplug.spotless'
spotless {
  kotlin {
    ktlint('0.39.0').userData([
        // TODO this should all come from editorconfig https://github.com/diffplug/spotless/issues/142
        'indent_size': '2',
        'kotlin_imports_layout': 'ascii',
    ])
  }
}
