buildscript {
  ext.versions = [
    'kotlin': '1.3.70',
    'serializationRuntime': '0.20.0',
    'retrofit': '2.7.2',
    'okhttp': '3.12.10',
    'junit': '4.13',
    'truth': '1.0',
  ]
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
  }
}

apply plugin: 'java-library'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'kotlinx-serialization'
apply from: file('gradle/gradle-mvn-push.gradle')

targetCompatibility = JavaVersion.VERSION_1_6
sourceCompatibility = JavaVersion.VERSION_1_6

repositories {
  mavenCentral()
  maven { url "https://kotlin.bintray.com/kotlinx" }
}

dependencies {
  api "com.squareup.retrofit2:retrofit:${versions.retrofit}"
  api "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
  api "org.jetbrains.kotlinx:kotlinx-serialization-runtime:${versions.serializationRuntime}"

  testImplementation "junit:junit:${versions.junit}"
  testImplementation "com.squareup.okhttp3:mockwebserver:${versions.okhttp}"
  testImplementation "com.google.truth:truth:${versions.truth}"
  testImplementation "org.jetbrains.kotlinx:kotlinx-serialization-protobuf:${versions.serializationRuntime}"
}
