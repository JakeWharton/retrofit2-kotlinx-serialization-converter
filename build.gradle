import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

buildscript {
  ext.versions = [
    'kotlin': '1.9.0',
    'serialization': '1.5.1',
  ]
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
    classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.25.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.27.0'
  }
}

apply plugin: 'java-library'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'com.vanniktech.maven.publish'

targetCompatibility = JavaVersion.VERSION_1_8
sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
  mavenCentral()
}

dependencies {
  api "com.squareup.retrofit2:retrofit:2.9.0"
  api "org.jetbrains.kotlinx:kotlinx-serialization-core:${versions.serialization}"

  testImplementation "junit:junit:4.13.2"
  testImplementation "com.squareup.okhttp3:mockwebserver:4.12.0"
  testImplementation "com.google.truth:truth:1.4.1"
  testImplementation "org.jetbrains.kotlinx:kotlinx-serialization-protobuf:${versions.serialization}"
  testImplementation "org.jetbrains.kotlinx:kotlinx-serialization-json:${versions.serialization}"
}

tasks.withType(KotlinJvmCompile).configureEach { task ->
  task.compilerOptions {
    jvmTarget = JvmTarget.JVM_1_8
    freeCompilerArgs.add(
      '-Xjsr305=strict',
    )
  }
}

apply plugin: 'com.diffplug.spotless'
spotless {
  kotlin {
    target("src/*/kotlin/**/*.kt")
    ktlint('0.48.1').editorConfigOverride([
      'ktlint_standard_filename': 'disabled',
    ])
  }
}
